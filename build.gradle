/*
 * Copyright (c) 2021. Dhyey Shah, Saurabh Pethani, Romil Nisar
 *
 * Developed by:
 *         Dhyey Shah<dhyeyshah4@gmail.com>
 *         https://github.com/dhyey-shah
 *
 * Contributors:
 *         Saurabh Pethani<spethani28@gmail.com>
 *         https://github.com/SaurabhPethani
 *
 *         Romil Nisar<rnisar7@gmail.com>
 *
 *
 * This file is part of Cafe.
 *
 * Cafe is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation,  version 3 of the License.
 *
 * Cafe is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Cafe.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    id 'distribution'
}

group 'cafe'
version '0.3.1'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-Xlint:all", "-Xlint:-serial", "-parameters"]
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {

    // https://mvnrepository.com/artifact/org.ow2.asm/asm
    implementation group: 'org.ow2.asm', name: 'asm', version: '7.3.1'

    // https://mvnrepository.com/artifact/org.ow2.asm/asm-util
    implementation group: 'org.ow2.asm', name: 'asm-util', version: '7.3.1'

    // https://mvnrepository.com/artifact/com.beust/jcommander
    implementation group: 'com.beust', name: 'jcommander', version: '1.78'

    // https://search.maven.org/artifact/org.fusesource.jansi/jansi/2.1.1/jar
    implementation 'org.fusesource.jansi:jansi:2.1.1'

    // https://mvnrepository.com/artifact/org.testng/testng
    testCompile group: 'org.testng', name: 'testng', version: '7.3.0'
}

distributions {
    main {
        baseName("cafe")
        contents {
            from(projectDir) {
                include('README*')
                include('COPYRIGHT*')
                include('LICENSE*')
                include('THIRD-PARTY')
            }
            into('examples') {
                from('examples') {
                    include '**/*.cafe'
                }
            }
        }
    }

}

startScripts {
    applicationName = 'cafe'
}

run {
    // RUN
    // args = ["-r", "-cp=.\\examples\\", "closure"]

    // Compile
    // args = ["-c",".\\examples\\Objects.cafe"]

    // Usage
    // args = ["--usage", "-c"]
}

test{
    useTestNG()
}

application {
    // Define the main class for the application.
    mainClassName = 'compiler.Main'
}

tasks.register("exe"){
    dependsOn tasks.installDist
    doLast {
        def innoSetupDir = new File("${buildDir}/innosetup")
        delete innoSetupDir
        innoSetupDir.mkdir();

        copy{
            from("${projectDir}/LICENSE", "${projectDir}/README.md")
            rename("LICENSE","LICENSE.txt")
            rename("README.md", "README.txt")
            into(innoSetupDir)
        }

        copy {
            from("${rootProject.projectDir}/gradle/innosetup/cafe_exe_compiler.iss")
            expand([
                    applicationVersion: "${project.version}",
            ])
            from("${rootProject.projectDir}/gradle/innosetup/logo.ico")
            into(innoSetupDir)
        }

        exec {
            workingDir rootProject.projectDir
            commandLine "iscc ${innoSetupDir}/cafe_exe_compiler.iss".split()
        }
    }
}